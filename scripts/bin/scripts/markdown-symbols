#!/usr/bin/env bb
;; -*- clojure -*-
;; vim: set filetype=clojure:

;; improvements:
;; - flatten


(require '[clojure.java.io :as io])
(require '[clojure.string :as str])

(def heading-pattern #"^(#+)\s*(.*)$")

(defn pad [n]
  (->> (repeat n "  ")
       (str/join "")))

(defn match-heading [line]
  (when-some [[_ level title] (re-matches heading-pattern line)]
    (str "interface!" (pad (dec (count level))) title)))

(defn parse-lines-xform [fname]
  (keep-indexed
   (fn [idx line]
     (when-some [heading (match-heading line)]
       (str fname ":" (inc idx) ":" heading)))))

(let [fname (first *command-line-args*)]
  (with-open [rdr (io/reader fname)]
    (let [lines (->> (line-seq rdr)
                     (sequence (parse-lines-xform fname)))]
      (doseq [line lines]
        (println line)))))

